import vayla.DownloadMetaDataTask

plugins {
  id 'java'
  id 'groovy'
  id "org.openapi.generator" version "6.2.1"
  id "de.undercouch.download" version "4.1.2"
}
repositories {
  mavenCentral()
}

sourceCompatibility = 11
targetCompatibility = 11

task downloadMetaData(type: DownloadMetaDataTask) {
  doFirst {
    mkdir "$buildDir"
  }
  fileName = "${buildDir}/metadata.json".toString()
}

defaultTasks 'generateApis'

def apiNames = ['projektirekisteri', 'aineistopalvelu', 'hakupalvelu']
def authorizationHeader = "Bearer ${System.env.VELHO_ACCESS_TOKEN}"

task generateApis

task downloadSwaggers {
  doLast {
    for (apiName in apiNames) {
      def apiVersion
      if (apiName == "projektirekisteri")
        apiVersion = "v2"
      else
        apiVersion = "v1"
      download {
        src "${System.env.VELHO_API_URL}/${apiName}/doc/${apiVersion}/swagger.json".toString()
        dest "${buildDir}/${apiName}.json".toString()
        headers = ["Authorization": authorizationHeader.toString()]
        retries = 5
      }
    }
  }
}

task downloadKuntaList(type: Download) {
  src 'http://rajapinnat.ymparisto.fi/api/Hakemistorajapinta/1.0/odata/Kunta'
  dest "${buildDir}/../../../common/generated/kunnat.json".toString()
  overwrite false
}

task downloadMaakuntaList(type: Download) {
  src 'http://rajapinnat.ymparisto.fi/api/Hakemistorajapinta/1.0/odata/Maakunta'
  dest "${buildDir}/../../../common/generated/maakunnat.json".toString()
  overwrite false
}

task downloadElyList(type: Download) {
  src 'http://rajapinnat.ymparisto.fi/api/Hakemistorajapinta/1.0/odata/Ely'
  dest "build/ely.json".toString()
  overwrite false
}

// This is a workaround for duplicate enum values in hakupalvelu. Remove this code after Velho has been fixed.
task fixSwagger(type: Copy) {
  from("${buildDir}") {
    include "*.json"
    filter { line -> line.replaceAll('"tieosuushaku","kohdeluokkahaku","kohdeluokkahaku","tieosuushaku"', '"tieosuushaku","kohdeluokkahaku"') }
  }
  into "${buildDir}/fixed".toString()
}

tasks.withType(org.openapitools.generator.gradle.plugin.tasks.GenerateTask) {
  outputs.upToDateWhen { false }
  outputs.cacheIf { false }
}

apiNames.each { name ->

  def generateApiTask = tasks.register("generate${name.capitalize()}", org.openapitools.generator.gradle.plugin.tasks.GenerateTask) {
    generatorName = "typescript-axios"
    inputSpec = "${buildDir}/fixed/${name}.json".toString()
    outputDir = "${buildDir}/../../../backend/src/velho/${name}"
    validateSpec = false
    skipOverwrite = false
    globalProperties = [
      supportsES6: "true",
      modelPropertyNaming: "original"
    ]
  }

  generateApis.dependsOn generateApiTask
}


generateApis.dependsOn downloadMetaData
generateApis.dependsOn fixSwagger
fixSwagger.dependsOn downloadSwaggers

generateApis.dependsOn downloadKuntaList
generateApis.dependsOn downloadMaakuntaList
generateApis.dependsOn downloadElyList
