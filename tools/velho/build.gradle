import org.openapitools.generator.gradle.plugin.tasks.GenerateTask
import vayla.DownloadMetaDataTask

plugins {
  id 'java'
  id 'groovy'
  id "org.openapi.generator" version "7.17.0"
  id "de.undercouch.download" version "5.6.0"
}
repositories {
  mavenCentral()
}

sourceCompatibility = 11
targetCompatibility = 11

task downloadMetaData(type: DownloadMetaDataTask) {
  doFirst {
    mkdir "$buildDir"
  }
  fileName = "${buildDir}/metadata.json".toString()
}

defaultTasks 'generateApis'

def apiDefinitions = [['projektirekisteri', "v2"], ['aineistopalvelu', "v1"], ['hakupalvelu', "v2"]]
def authorizationHeader = "Bearer ${System.env.VELHO_ACCESS_TOKEN}"

task generateApis

task downloadSwaggers {
  doLast {
    for (apiDefinition in apiDefinitions) {
      def apiName = apiDefinition[0]
      def apiVersion = apiDefinition[1]
      download.run {
        src "${System.env.VELHO_API_URL}/${apiName}/doc/${apiVersion}/swagger.json".toString()
        dest "${buildDir}/${apiName}.json".toString()
        headers = ["Authorization": authorizationHeader.toString()]
        retries = 5
      }
    }
  }
}

task downloadKuntaList(type: Download) {
  src 'http://rajapinnat.ymparisto.fi/api/Hakemistorajapinta/1.0/odata/Kunta'
  dest "build/kunnat.json".toString()
  overwrite false
}

task downloadMaakuntaList(type: Download) {
  src 'http://rajapinnat.ymparisto.fi/api/Hakemistorajapinta/1.0/odata/Maakunta'
  dest "build/maakunnat.json".toString()
  overwrite false
}

task downloadElyList(type: Download) {
  src 'http://rajapinnat.ymparisto.fi/api/Hakemistorajapinta/1.0/odata/Ely'
  dest "build/ely.json".toString()
  overwrite false
}

tasks.withType(GenerateTask) {
  outputs.upToDateWhen { false }
  outputs.cacheIf { false }
}

apiDefinitions.each { apiDefinition ->
  def apiName = apiDefinition[0]
  def generateApiTask = tasks.register("generate${apiName.capitalize()}", org.openapitools.generator.gradle.plugin.tasks.GenerateTask) {
    generatorName = "typescript-axios"
    inputSpec = "${buildDir}/${apiName}.json".toString()
    outputDir = "${buildDir}/../../../backend/src/velho/${apiName}"
    validateSpec = false
    skipOverwrite = false
    globalProperties = [
        models          : "",      // generoi mallit
        apis            : "",      // generoi API-luokat
        supportingFiles : "",      // generoi supporting files, koska typescript-axios vaatii ne
        modelDocs       : "false", // ei luo model-dokumentaatiota
        apiDocs         : "false", // ei luo API-dokumentaatiota
        modelTests      : "false", // ei luo model-testitiedostoja
        apiTests        : "false"  // ei luo API-testitiedostoja
    ]
    configOptions = [
      modelPropertyNaming: "original",
      enumPropertyNaming: "original"
    ]
  }

  generateApis.dependsOn generateApiTask
}


generateApis.dependsOn downloadMetaData
generateApis.dependsOn downloadSwaggers

generateApis.dependsOn downloadKuntaList
generateApis.dependsOn downloadMaakuntaList
generateApis.dependsOn downloadElyList
